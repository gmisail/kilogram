extern panic: (int) -> int
extern print: (string) -> int
extern int_to_string: (int) -> string

enum Sequence
	Cons(int, Sequence),
	Nil
end

let identity = function<T>(val: T): T 
	val
end

let rec length = function(seq: Sequence): int
	case seq of
		Cons(num, tail) -> 1 + length(tail),
		Nil -> 0
	end
end

let rec map = function(seq: Sequence, f: (int) -> int): Sequence
    case seq of
        Cons(num, tail) -> Cons(f(num), map(tail, f)),
        Nil -> Nil
    end
end

let rec filter = function(seq: Sequence, f: (int) -> bool): Sequence 
	case seq of
		Cons(num, tail) -> 
			let filtered_tail = filter(tail, f)
			if f(num) then Cons(num, filtered_tail) 
			else filtered_tail,
		Nil -> seq
	end
end

let rec print_list = function(seq: Sequence): int
    case seq of
        Cons(num, tail) ->
            let res = print(int_to_string(num))
            print_list(tail),
        Nil -> 0
    end
end

let initial_list = Cons(1, Cons(2, Cons(3, Cons(4, Cons(5, Nil)))))

print_list(
    filter(
        initial_list,
        function(i: int): bool 
            i == 3 
        end
    )
)
